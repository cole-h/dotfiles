{ lib, pkgs, ... }:
{
  boot.zfs.enableUnstable = true;
  boot.kernelPackages = lib.mkForce pkgs.linuxKernel.packages.linux_6_2;
  boot.kernelParams = [
    "module_blacklist=i915"
  ];

  boot.kernelPatches = [
    {
      name = "nouveau.patch";
      # Generated by doing the following:
      # 1. git clone https://gitlab.freedesktop.org/skeggsb/nouveau.git
      # 2. git checkout 01.03-gsp-rm
      # 2. git remote add stable https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git
      # 3. git format-patch f34dd826b7be8caa06d8cd088385cf457a0cc3b1^ -o nvidia
      # NOTE: if it gets pushed to again, above commit is "WIPdrm/nouveau/fb: split fb sizing from mm construction", the "first" commit in the series
      # NOTE: there is a prior commit, 85763d9e5a381f2aadab24c5b1484865edddfb82 ("drm/nouveau/fb/gp102-: cache scrubber binary on first load"), but that has already made it to 6.2.7
      # 4. git fetch stable --tags
      # 5. git checkout v6.2.7
      # NOTE: or whatever the latest kernel is (in nixpkgs) for the patchset (check the Makefile -- at time of writing, it was 6.3.0-rc1, but it applied cleanly to 6.2.7)
      # 6. git am nvidia/*.patch
      # NOTE: you may need to resolve conflicts -- make sure the commit looks roughly the same before and after the rebase
      # 7. git format-patch v6.2.7 --stdout > nouveau.patch
      # NOTE: into one file because `boot.kernelPatches` doesn't like a super long list of patches
      # NOTE: you will be rebuilding the kernel and anything that depends on it
      patch = ./nouveau.patch;
    }
  ];

  hardware.opengl.package = ((pkgs.mesa.overrideAttrs ({ mesonFlags ? [ ], ... }: {
    version = "23.0.0-devel";
    src = pkgs.fetchgit {
      url = "https://gitlab.freedesktop.org/skeggsb/mesa.git";
      rev = "57081b4692a1d871c8e24c92afdbeb0cfc29d32b"; # branch "02.00-ad10x"
      sha256 = "sha256-e+ReIIcLr2DHgJNbPJrWbYSQZmWGLs0raBNHsdTCS80=";
    };

    mesonFlags = lib.lists.remove "-Dintel-clc=enabled" mesonFlags;
  })).override { enableOpenCL = false; }).drivers;

  hardware.firmware = [
    (pkgs.stdenv.mkDerivation {
      pname = "nvidia-gsp-firmware";
      version = "525.60.11";
      src = pkgs.fetchurl {
        url = "https://us.download.nvidia.com/XFree86/Linux-x86_64/525.60.11/NVIDIA-Linux-x86_64-525.60.11.run";
        sha256 = "sha256-gW7mwuCBPMw9SnlY9x/EmjfGDv4dUdYUbBznJAOYPV0=";
      };

      dontBuild = true;
      dontFixup = true;
      dontStrip = true;

      unpackPhase = ''
        sh $src --extract-only --target build
        cd build
      '';

      installPhase = ''
        mkdir -p $out/lib/firmware/nvidia/ad102/gsp
        mv firmware/gsp_ad10x.bin $out/lib/firmware/nvidia/ad102/gsp/gsp-5256011.bin
      '';
    })
  ];
}
